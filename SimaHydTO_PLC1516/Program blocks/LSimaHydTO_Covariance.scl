FUNCTION "LSimaHydTO_Covariance" : Int
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : APC_ERLF
FAMILY : LSimaHydTO
VERSION : 1.0
   VAR_INPUT 
      firstIdx : Int;
      numOfPoints : Int;
   END_VAR

   VAR_OUTPUT 
      factor : Real;
      correlation : Real;
   END_VAR

   VAR_IN_OUT 
      valuesX : Array[*] of Real;
      valuesY : Array[*] of Real;
   END_VAR

   VAR_TEMP 
      i : Int;
      ret : Int;
      sum2X : Real;
      sum2Y : Real;
      meanValueX : Real;
      meanValueY : Real;
      sumX : Real;
      sumY : Real;
      varianceX : Real;
      variancey : Real;
      sumCoVar : Real;
      covariance : Real;
      tempLB : Int;
      tempUB : Int;
      lastIdx : Int;
   END_VAR


BEGIN
	//================================================================================
	//SIEMENS AG // (c)Copyright 2019 All Rights Reserved
	//--------------------------------------------------------------------------------
	//Library:       LSimaHydTO
	//Tested with:   S7-15xx
	//Engineering:   TIA Portal  V15
	//Restrictions:  -
	//Requirements:  S7-1500 (FW 2.5)
	//Functionality: HMI support program - Internal use  
	//
	//--------------------------------------------------------------------------------
	//Change log table:
	//
	//Version     Date          Expert in charge    Changes applied
	//01.00.00    15.03.2018    APC_ERLF            First released version
	//================================================================================
	
	
	IF #numOfPoints < 2 THEN
	  #LSimaHydTO_Covariance := -1;
	  RETURN;
	END_IF;
	#tempLB := DINT_TO_INT(MAX(IN1 := LOWER_BOUND(ARR:=#valuesX, DIM:=1),IN2 := LOWER_BOUND(ARR:=#valuesY, DIM:=1)));
	#tempUB := DINT_TO_INT(MIN(IN1 := UPPER_BOUND(ARR := #valuesX, DIM := 1), IN2 := UPPER_BOUND(ARR := #valuesY, DIM := 1)));
	#lastIdx := #firstIdx + #numOfPoints - 1;
	IF #tempLB > #firstIdx  THEN
	  #ret := -2;
	ELSIF #tempUB < #lastIdx THEN
	  #ret := -3;
	ELSE
	  #ret := 0;
	  #sumX := 0.0;
	  #sumY := 0.0;
	  #sum2X := 0.0;
	  #sum2Y := 0.0;
	  #sumCoVar := 0.0;
	  FOR #i := #firstIdx TO #lastIdx DO
	    #sumX := #sumX + #valuesX[#i];
	    #sumY := #sumY + #valuesY[#i];
	  END_FOR;
	  #meanValueX := #sumX / #numOfPoints;
	  #meanValueY := #sumY / #numOfPoints;
	  FOR #i := #firstIdx TO #lastIdx DO
	    #sum2X := #sum2X + SQR(#valuesX[#i] - #meanValueX);
	    #sum2Y := #sum2Y + SQR(#valuesY[#i] - #meanValueY);
	    #sumCoVar := #sumCoVar + (#valuesX[#i] - #meanValueX) * (#valuesY[#i] - #meanValueY);
	  END_FOR;
	  #varianceX := #sum2X / (#numOfPoints - 1);
	  #variancey := #sum2Y / (#numOfPoints - 1);
	  #covariance := #sumCoVar / (#numOfPoints - 1);
	  #factor := #covariance / #varianceX;
	  #correlation := #covariance / (SQRT(#varianceX) * SQRT(#variancey));
	END_IF;
	#LSimaHydTO_Covariance := #ret;
	
	
END_FUNCTION

